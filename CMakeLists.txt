cmake_minimum_required(VERSION 3.24)
project(cpputil CXX)

set(CMAKE_CXX_STANDARD            23)
set(CMAKE_CXX_STANDARD_REQUIRED   ON)
set(CMAKE_CXX_EXTENSIONS          OFF)
set(CMAKE_EXPORT_COMPILE_COMMANDS ON)

function (configure_warnings target)
    if (MSVC)
        target_compile_options(${target} PRIVATE "/W4")
    else ()
        target_compile_options(${target} PRIVATE "-Wall" "-Wextra" "-Wpedantic")
    endif ()
endfunction ()

function (cpputil_library name)
    add_library(cpputil_${name})
    add_library(cpputil::${name} ALIAS cpputil_${name})
    target_include_directories(cpputil_${name} PUBLIC ${PROJECT_SOURCE_DIR}/src/${name})
    configure_warnings(cpputil_${name})
endfunction ()

add_subdirectory(src/util)
add_subdirectory(src/io)
add_subdirectory(src/input)
add_subdirectory(src/json)
